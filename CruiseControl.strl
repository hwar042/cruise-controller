% Main Module %
module CruiseControl:

% Inputs %
input On, Off, Resume, Set, QuickDecel, QuickAccel;
input Accel : float;
input Brake : float;
input Speed : float;

% Outputs %
output CruiseSpeed : float;
output Throttle : float;
output CruiseState : integer;
% 1:OFF 2:ON 3:STDBY 4:DISABLE %

signal state : integer in
	[loop
		present state then
			emit CruiseState(?state);
			end present;
			pause;
	end loop]
	||
	run CruiseControlController[ signal On/On1;
								 signal Off/Off1;
								 signal Resume/Resume1;
								 signal Accel/Accel1;
								 signal Brake/Brake1;
								 signal Speed/Speed1;
								 signal state/CruiseState ]
	||
	run CruiseSpeedController[ signal Set/Set_speed;
							   signal QuickAccel/QuickAccel_speed;
							   signal QuickDecel/QuickDecel_speed;
							   signal Accel/Accel_speed;
							   signal state/State_speed;
							   signal Speed/Speed_speed;
							   signal CruiseSpeed/CruiseSpeed_speed ]
	||
	run ThrottleController[ signal Accel/Accel_driving;
							signal CruiseSpeed/CruiseSpeed_driving;
							signal state/State_driving;
							signal Speed/Speed_driving;
							signal Throttle/Throttle_driving ]
end signal

end module

% SubModules %

module CruiseControlController:
    constant PedalsMin : float;
    constant SpeedMin : float;
    constant SpeedMax : float;

    input On1;
    input Off1;
    input Resume1;
    input Accel1 : float;
    input Brake1 : float;
    input Speed1 : float;

    output CruiseState : integer;

    % OFF-1 ON-2 STDBY-3 DIS-4
    var state := 1 : integer in
        loop
            emit CruiseState(state);
            pause;
            trap T2 in
                % OFF STATE LOGIC
                if state = 1 then
                    present On1 then
                        state := 2; exit T2;
                    end present;
                end if;
                % ON STATE LOGIC
                if state = 2 then
                    present Off1 then state := 1 end;
                    if ?Accel1 > PedalsMin then
                        state := 4; exit T2;
                    end if;
                    if ?Speed1 < SpeedMin then
                        state := 4; exit T2;
                    end if;
                    if ?Speed1 > SpeedMax then
                        state := 4; exit T2;
                    end if;
                    if ?Brake1 > PedalsMin then
                        state := 3; exit T2;
                    end if;
                    exit T2;
                end if;
                % STANDBY STATE LOGIC
                if state = 3 then
                    present Off1 then
                        state := 1; exit T2;
                    end present;
                    present Resume1 then
                        if ?Accel1 > PedalsMin then
                            state := 4; exit T2;
                        end if;
                        if ?Speed1 < SpeedMin then
                            state := 4; exit T2;
                        end if;
                        if ?Speed1 > SpeedMax then
                            state := 4; exit T2;
                        end if;
                        if (?Speed1 > SpeedMin) and (?Speed1 < SpeedMax) then
                            state := 2; exit T2;
                        end if;
                    end present;
                    exit T2;
                end if;
                % DISABLE STATE LOGIC
                if state = 4 then
                    present Off1 then
                        state := 1; exit T2;
                    end present;
                    if (?Accel1 < PedalsMin) and (?Speed1 > SpeedMin) and (?Speed1 < SpeedMax) then
                        state := 2; exit T2;
                    end if;
                    exit T2;
                end if;
            end trap
        end loop
    end var
end module

module CruiseSpeedController:
    %{
        The cruising speed (CruiseSpeed) shall be managed only when the cruise control is enabled, meaning when it is in the ON, STBDY, or DISABLE states.
        The cruise speed shall be set to the current speed when the On button is pressed initially, and whenever the Set button is pressed thereafter.
        The cruise speed shall be increased by SpeedInc km/h when the QuickAccel button is pressed, provided that this new value of the cruise speed is still lower than the maximal speed, SpeedMax km/h.
        The cruise speed shall be decreased by SpeedInc km/h when the QuickDecel button is pressed, provided that this new value of the cruise speed is still higher than the minimal speed, SpeedMin km/h.
        The cruise speed shall be maintained between SpeedMin and SpeedMax km/h. This means that whenever an attempt is made to set the cruise speed (either through the On, Set, QuickAccel, or QuickDecel buttons) to a value less than SpeedMin or greater than SpeedMax, the cruise speed shall be limited to either SpeedMin or SpeedMax, respectively.
    }%

    % Constants %
    constant SpeedMin : float;
    constant SpeedMax : float;
    constant SpeedMinInc : float;
    constant SpeedMaxInc : float;
    constant SpeedInc : float;
    constant Zero : float;

    % Inputs %
    input Set_speed;
    input QuickAccel_speed;
    input QuickDecel_speed;
    input Accel_speed : float;
    input State_speed : integer;
    input Speed_speed : float;

    % Outputs %
    output CruiseSpeed_speed : float;

    var CurrentSpeed := Zero : float in
	loop
		emit CruiseSpeed_speed(CurrentSpeed);
		pause;
		trap T_speed in
			% Logic For When Cruise Control Off %
			if ?State_speed = 1 then
				CurrentSpeed := Zero;
				exit T_speed;
			% Logic For When Cruise Control On %
			else
				present Set_speed then
                    if ((CurrentSpeed > SpeedMin) and (CurrentSpeed < SpeedMax)) then
                        CurrentSpeed := ?Speed_speed;
                        exit T_speed;
                    end if;
				end present;
				present QuickAccel_speed then
					if CurrentSpeed < SpeedMaxInc then
						CurrentSpeed := CurrentSpeed + SpeedInc;
						exit T_speed;
					end if;
				end present;
				present QuickDecel_speed then
					if CurrentSpeed > SpeedMinInc then
						CurrentSpeed := CurrentSpeed - SpeedInc;
						exit T_speed;
					end if;
				end present;
				if ((?State_speed = 2) and (pre(?State_speed) = 1)) then
                    if ((CurrentSpeed > SpeedMin) and (CurrentSpeed < SpeedMax)) then
                        CurrentSpeed := ?Speed_speed;
                        exit T_speed;
                    end if;
				end if;
				exit T_speed;
			end if;
		end trap
	end loop
end var
end module

module ThrottleController:
    %{
        When the cruise control is off, the car speed shall be driven by the accelerator pedal.
        When the cruise control is on, the car speed shall be automatically regulated.
        The regulation shall be done using a proportional and integral algorithm, with Kp and Ki factors.
    }%

    % Inputs %
    input Accel_driving : float;
    input CruiseSpeed_driving : float;
    input State_driving : integer;
    input Speed_driving : float;
    output Throttle_driving : float;

    function regulateThrottle(integer, float, float) : float;

    loop
        pause;
        trap T_driving in
            if ?State_driving = 1 then
                emit Throttle_driving(?Accel_driving);
                exit T_driving;
            elsif ((?State_driving = 2) and (pre(?State_driving) = 1)) then
                emit Throttle_driving(regulateThrottle(1, ?CruiseSpeed_driving, ?Speed_driving));
                exit T_driving;
            elsif ?State_driving = 2 then
                emit Throttle_driving(regulateThrottle(0, ?CruiseSpeed_driving, ?Speed_driving));
                exit T_driving;
            else
                emit Throttle_driving(?Accel_driving);
                exit T_driving;
            end if;
        end trap
    end loop
end module
